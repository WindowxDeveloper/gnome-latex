CMAKE_MINIMUM_REQUIRED(VERSION 2.6.4)

PROJECT(latexila)

# version
SET(latexila_MAJOR 0)
SET(latexila_MINOR 2)
SET(latexila_PATCH 0)
SET(latexila_VERSION ${latexila_MAJOR}.${latexila_MINOR}.${latexila_PATCH})

MESSAGE(STATUS "*** Building LaTeXila ${latexila_VERSION} ***")

IF(NOT DATA_DIR)
  SET(DATA_DIR "${CMAKE_INSTALL_PREFIX}/share/latexila"
	  CACHE PATH "The directory where the data is")
ENDIF(NOT DATA_DIR)

OPTION(ENABLE_NLS "Native Language Support" ON)

IF(ENABLE_NLS)
  SET(LATEXILA_NLS_ENABLED TRUE)
  SET(LATEXILA_NLS_PACKAGE "latexila")
  SET(LATEXILA_NLS_LOCALEDIR "${CMAKE_INSTALL_PREFIX}/share/locale")
  MESSAGE(STATUS "Native language support enabled.")
  FIND_PACKAGE(Gettext REQUIRED)
  FILE(GLOB latexila_PO_FILES ${latexila_SOURCE_DIR}/po/*.po)
  GETTEXT_CREATE_TRANSLATIONS(po/latexila.pot ALL ${latexila_PO_FILES})
ELSE(ENABLE_NLS)
  MESSAGE(STATUS "Native language support disabled.")
ENDIF(ENABLE_NLS)

SET(CMAKE_C_FLAGS "-Wall -std=c99 -DGTK_DISABLE_DEPRECATED=1")

FIND_PACKAGE(PkgConfig REQUIRED)
PKG_CHECK_MODULES(GTK2 REQUIRED gtk+-2.0>=2.16 gtksourceview-2.0>=2.4)
SET(GTKSOURCEVIEW_VERSION ${GTK2_gtksourceview-2.0_VERSION})

# commands

SET(COMMAND_LATEX "latex")
SET(COMMAND_PDFLATEX "pdflatex")
SET(COMMAND_DVIPDF "dvipdf")
SET(COMMAND_DVIPS "dvips")
SET(COMMAND_BIBTEX "bibtex")
SET(COMMAND_MAKEINDEX "makeindex")

FIND_PACKAGE(LATEX)

IF(LATEX_COMPILER)
  SET(COMMAND_LATEX ${LATEX_COMPILER})
ENDIF(LATEX_COMPILER)

IF(PDFLATEX_COMPILER)
  SET(COMMAND_PDFLATEX ${PDFLATEX_COMPILER})
ENDIF(PDFLATEX_COMPILER)

IF(DVIPDF_CONVERTER)
  SET(COMMAND_DVIPDF ${DVIPDF_CONVERTER})
ENDIF(DVIPDF_CONVERTER)

IF(DVIPS_CONVERTER)
  SET(COMMAND_DVIPS ${DVIPS_CONVERTER})
ENDIF(DVIPS_CONVERTER)

IF(BIBTEX_COMPILER)
  SET(COMMAND_BIBTEX ${BIBTEX_COMPILER})
ENDIF(BIBTEX_COMPILER)

IF(MAKEINDEX_COMPILER)
  SET(COMMAND_MAKEINDEX ${MAKEINDEX_COMPILER})
ENDIF(MAKEINDEX_COMPILER)

MESSAGE(STATUS "Commands: " ${COMMAND_LATEX} " ; " ${COMMAND_PDFLATEX} " ; "
	${COMMAND_DVIPDF} " ; " ${COMMAND_DVIPS} " ; " ${COMMAND_BIBTEX} " ; "
	${COMMAND_MAKEINDEX})


INCLUDE_DIRECTORIES(${GTK2_INCLUDE_DIRS} ${latexila_BINARY_DIR}/src)

CONFIGURE_FILE(${latexila_SOURCE_DIR}/src/config.h.cmake
	${latexila_BINARY_DIR}/src/config.h)

ADD_SUBDIRECTORY(src)
ADD_SUBDIRECTORY(data)
